
dependencies {
    compile project(':demo:applet')
    compile project(':dria:dria-swing')
    testCompile "org.easytesting:fest-swing:$festSwingVersion"
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } // Para generar un jar con todas las dependencias.
    manifest {
        attributes(
                'Application-Name': 'Testing Applet with DRIA',
                'Implementation-version': version,
                'Permissions': 'sandbox',
                'Codebase': 'localhost',
                'Application-Library-Allowable-Codebase': 'localhost',
                'Caller-Allowable-Codebase': 'localhost'
        )
    }
}

task signJar(dependsOn: jar) << {
    ant.signjar(
            jar: jar.archivePath,
            keystore: '../../certs/testing-applet.jks',
            storepass: 'autentia',
            alias: 'demoCert1'
    )
}

task copyJarToWebappSourceDir(type: Copy, dependsOn: signJar) {
    from jar.archivePath
    into '../system-test/src/main/webapp/'
}
test.dependsOn copyJarToWebappSourceDir
